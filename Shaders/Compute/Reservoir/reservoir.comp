#include "../Random/random.comp"

struct ReservoirHitInfo
{
    vec4 normalDepth;
    vec4 impactPoint;
    vec4 samplePoint;
};

struct Reservoir
{
    vec4 item;

    float itemWeight;
    float totalWeight;

    uint numItems;
    uint state;
};

Reservoir CreateReservoir()
{
    Reservoir reservoir;
    reservoir.totalWeight = 0.0;
    reservoir.numItems = 0;
    return reservoir;
}

float ReservoirCorrection(Reservoir reservoir)
{
    return reservoir.totalWeight / (reservoir.numItems * reservoir.itemWeight);
}

vec4 GetReservoirValue(Reservoir reservoir)
{
    return reservoir.item * ReservoirCorrection(reservoir);
}

bool AddToReservoir(inout Reservoir reservoir, vec4 item, float itemWeight, uint seedState, inout uint state)
{
    reservoir.totalWeight += itemWeight;
    reservoir.numItems++;

    bool accepted = RandomValue(state) <= itemWeight / max(reservoir.totalWeight, EPS);
    if (reservoir.numItems == 1 || accepted)
    {
        reservoir.item = item;
        reservoir.itemWeight = itemWeight;
        reservoir.state = seedState;
    }

    return accepted;
}

void MergeReservoirs(inout Reservoir source, Reservoir target, float weight, inout uint state)
{
    uint numItems = source.numItems + target.numItems;
    AddToReservoir(
        source, 
        target.item, 
        weight * target.numItems * ReservoirCorrection(target), 
        target.state,
        state
    );

    source.numItems = numItems;
}