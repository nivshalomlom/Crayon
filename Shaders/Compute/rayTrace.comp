#version 430 core

#include "./Rays/ray_intersections.comp"

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
layout(rgba32F, binding = 0) uniform image2D renderTarget;

uniform uint frameCounter;

vec4 Trace(Ray ray, uint bounceLimit, inout uint state)
{
    vec3 incomingLight = vec3(0.0);
    vec3 rayColor = vec3(1.0);

    RayHit rayHit;
    for (uint i = 0; i <= bounceLimit; i++)
    {
        if (Vec3Max(rayColor) <= EPS)
            break;

        if (FireRay(ray, rayHit))
        {
            Material material = rayHit.material;
            vec3 normal = rayHit.impact.normal;
            vec3 emission = Emission(material);

            BRDFPoint point = CreateBRDFPoint(
                -ray.direction,
                normalize(rayHit.impact.normal + RandomDirection(state)),
                normal
            );

            ray.origin = rayHit.impact.point + normal * 0.0001;
            ray.direction = point.lightDirection;

            incomingLight += emission * rayColor;
            rayColor *= BRDF(point, material);
        }
        else
        {
            incomingLight += EnvironmentLight(ray.direction) * rayColor;
            break;
        }
    }

    return vec4(incomingLight, 1.0);
}

void UpdateImage(ivec2 coords, vec4 value)
{
    vec4 prev = imageLoad(renderTarget, coords);
    vec4 avg = (prev * frameCounter + value) / (frameCounter + 1);
    imageStore(renderTarget, coords, avg);
}

void main()
{
    ivec2 dimensions = imageSize(renderTarget);
    ivec2 coords = ivec2(gl_GlobalInvocationID.xy);

    if (coords.x >= dimensions.x || coords.y >= dimensions.y)
        return;

    uint state = coords.x + coords.y * dimensions.x + frameCounter * 6647369;
    Reservoir reservoir = CreateReservoir();

    for (int i = 0; i < raysPerPixel; i++)
    {
        vec2 uv = vec2(coords + RandomVector2(state)) / dimensions;
        Ray ray = CreateCameraRayFromUV(uv);
        
        vec4 rayColor = Trace(ray, rayBounces, state);
        AddToReservoir(reservoir, rayColor, dot(rayColor, rayColor), state);
    }

    UpdateImage(coords, GetReservoirValue(reservoir));
}