#version 430 core

#include "./Utility/constants.comp"
#include "./Geometry/transform.comp"
#include "./Rays/ray.comp"
#include "./Buffers/reservoir.comp"
#include "./Buffers/camera.comp"

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

const ivec2 offsets[8] = {
    ivec2( 1,  0),
    ivec2(-1,  0),
    ivec2( 0,  1),
    ivec2( 0, -1),
    ivec2( 1, -1),
    ivec2(-1,  1),
    ivec2(-1, -1),
    ivec2( 1,  1)
};

uniform ivec2 dimensions;
uniform int samplingDistance;

void main()
{
    ivec2 coords = ivec2(gl_GlobalInvocationID.xy);

    if (coords.x >= dimensions.x || coords.y >= dimensions.y)
        return;

    uint kernal_ID = coords.x + coords.y * dimensions.x;
    uint state = kernal_ID + Camera.frameCounter * 6647369;
    Reservoir reservoir = CreateReservoir(reservoirs[kernal_ID]);

    vec3 normal = reservoir.normalDepth.xyz;
    float depth = reservoir.normalDepth.w;

    for (int i = 0; i < offsets.length(); i++)
    {
        if (RandomValue(state) < 0.5)
            continue;

        ivec2 neighbor = coords + samplingDistance * offsets[i];
        uint neighbor_ID = neighbor.x + neighbor.y * dimensions.x;
        
        if (neighbor_ID >= reservoirs.length())
            continue;

        float neighborDepth = reservoirs[neighbor_ID].normalDepth.w;
        if (neighborDepth < 0 || abs(depth - neighborDepth) > 0.1 * depth)
            continue;

        if (dot(normal, reservoirs[neighbor_ID].normalDepth.xyz) < 0.906)
            continue;

        MergeReservoirs(reservoir, reservoirs[neighbor_ID], state);
    }

    barrier();
    reservoirs[kernal_ID] = reservoir;
}